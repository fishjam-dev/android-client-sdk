plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id "org.jetbrains.dokka"
    id 'org.jmailen.kotlinter'
    id "com.google.protobuf" version "0.9.3"
}

android {
    namespace 'com.jellyfishdev.jellyfishclient'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

ext {
    customFooterMessage = "Copyright 2023, Software Mansion."
}

afterEvaluate {
    tasks.named("dokkaHtml") {
        pluginsMapConfiguration.set([
                "org.jetbrains.dokka.base.DokkaBase": """{
                "footerMessage": "$customFooterMessage"
            }"""
        ])
    }
}

tasks.named("dokkaHtml") {
    moduleName.set("JellyfishClient")
    dokkaSourceSets {
        named("main") {
            includes.from("module.md")
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    testImplementation 'junit:junit:4.13.2'
    api 'com.github.jellyfish-dev:membrane-webrtc-android:jellyfish-SNAPSHOT'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.google.protobuf:protobuf-kotlin:3.23.1'
}