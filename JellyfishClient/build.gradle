plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id "org.jetbrains.dokka"
    // id 'org.jmailen.kotlinter'
    id "com.google.protobuf" version "0.9.3"
    id 'maven-publish'

    id "kotlin-android"
    id "kotlin-kapt"
}

android {
    namespace 'com.jellyfishdev.jellyfishclient'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility java_version
        targetCompatibility java_version
    }
    kotlinOptions {
        jvmTarget = java_version
    }

    tasks.withType(org.jetbrains.kotlin.gradle.internal.KaptGenerateStubsTask).configureEach {
        kotlinOptions.jvmTarget = java_version
    }
}

ext {
    customFooterMessage = "Copyright 2023, Software Mansion."
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'com.jellyfishdev.jellyfishclient'
                artifactId = 'jellyfish-android-client'
                version = '0.1.0'
            }
        }
    }
    tasks.named("dokkaHtml") {
        pluginsMapConfiguration.set([
                "org.jetbrains.dokka.base.DokkaBase": """{
                "footerMessage": "$customFooterMessage"
            }"""
        ])
    }
}

tasks.named("dokkaHtml") {
    moduleName.set("JellyfishClient")
    dokkaSourceSets {
        named("main") {
            includes.from("module.md")
        }
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    testImplementation 'junit:junit:4.13.2'
    // api 'com.github.jellyfish-dev:membrane-webrtc-android:5.0.0'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.google.protobuf:protobuf-kotlin:3.23.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation deps.coroutines.lib
    implementation deps.timber
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.github.dsrees:JavaPhoenixClient:1.0.0'
    implementation 'androidx.core:core:1.8.0'


    api 'io.github.webrtc-sdk:android:104.5112.09'
    implementation 'com.google.dagger:dagger:2.38'
    kapt 'com.google.dagger:dagger-compiler:2.38'

    testImplementation "io.mockk:mockk:1.13.2"
}