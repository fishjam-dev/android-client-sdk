plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id "org.jetbrains.dokka"
    id "com.google.protobuf" version "0.9.4"
    id 'maven-publish'

    id "kotlin-android"
}

android {
    namespace 'com.fishjam.client'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility java_version
        targetCompatibility java_version
    }
    kotlinOptions {
        jvmTarget = java_version
    }
}

ext {
    customFooterMessage = "Copyright 2023, Software Mansion."
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'com.fishjam.client'
                artifactId = 'fishjam-android-client'
                version = '0.1.0'
            }
        }
    }
    tasks.named("dokkaHtml") {
        pluginsMapConfiguration.set([
                "org.jetbrains.dokka.base.DokkaBase": """{
                "footerMessage": "$customFooterMessage"
            }"""
        ])
    }
}

tasks.named("dokkaHtml") {
    moduleName.set("FishjamClient")
    dokkaSourceSets {
        named("main") {
            includes.from("module.md")
        }
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.google.protobuf:protobuf-kotlin:4.26.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0"
    implementation "com.github.ajalt:timberkt:1.5.1"
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.github.dsrees:JavaPhoenixClient:1.0.0'
    implementation 'androidx.core:core:1.8.20'


    api 'io.github.webrtc-sdk:android:104.5112.09'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk:1.13.2"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0"
    testImplementation "io.mockk:mockk:1.13.2"
    testImplementation 'org.khronos:opengl-api:gl1.1-android-2.1_r1'

}